SELECT 1
USE BCCTAS INDEX BCCTAS
SELECT 2
USE BCOPERA INDEX BCOPERA3,BCOPERA4
SET ORDER TO 2
STORE .T. TO WCON1
DO WHILE WCON1
   SET COLOR TO
   @ 04,00 CLEAR
   @ 04,00 SAY "CONCILIACION BANCARIA (INFORME)"
   @ 05,00 TO 10,50
   @ 06,01 SAY "CUENTA :"
   @ 07,01 SAY "A"+CHR(165)+"O    :"
   @ 08,01 SAY "MES    :"
   @ 09,01 SAY "SALIDA :"
   @ 06,10 GET WCUENTA
   READ
   IF WCUENTA=SPACE(6).OR.READKEY()=12.OR.READKEY()=268
      EXIT
   ENDIF
   SELECT 1
   SEEK WCUENTA
   IF EOF()
      STORE "CUENTA NO REGISTRADA, VERIFIQUE." TO MES
      DO AVISO WITH MES
      LOOP
   ELSE
      STORE DESCRI TO WCUENTADES
   ENDIF
   @ 06,18 SAY WCUENTADES
   STORE YEAR(DATE())  TO WYEAR
   STORE MONTH(DATE()) TO WMONTH

   @ 07,10 GET WYEAR PICTURE "9999"
   READ
   IF WYEAR=0.OR.READKEY()=12.OR.READKEY()=268
      LOOP
   ENDIF
   @ 08,10 GET WMONTH PICTURE "99"
   READ
   IF WMONTH<=0.OR. WMONTH>12.OR.READKEY()=12.OR.READKEY()=268
      LOOP
   ENDIF

   STORE "SELECCIONE LA SALIDA: (M)ONITOR, (I)MPRESORA" TO TEX
   STORE "MI" TO WCH
   DO PREGUNTA
   IF READKEY()=12 .OR. READKEY()=268
      LOOP
   ENDIF
   STORE WCH TO WSALIDA
   IF WSALIDA = "I"
      STORE 55 TO WSALTO
      STORE "IMPRESORA" TO WSALIDES
   ELSE
      STORE 20 TO WSALTO
      STORE "MONITOR" TO WSALIDES
   ENDIF
   @ 09,10 SAY WSALIDES

   STORE "OPCIONES: (C)ONTINUAR, (S)ALIR" TO TEX
   STORE "CS" TO WCH
   DO PREGUNTA
   IF WCH = "S"
      LOOP
   ENDIF
   STORE 100 TO WLINE
   STORE 0   TO WPAGE

   *** SALDO INICIAL DEL MES SEGUN BANCO
   *** TOMA SOLO REGISTROS CONCILIADOS DE MESES ANTERIORES
   STORE 0 TO WSALDOINI
   SELECT 2
   SEEK WCUENTA
   DO WHILE .NOT. EOF() .AND. CTA = WCUENTA ;
            .AND. ((YEAR(ESTATFECHA)<WYEAR) .OR. (YEAR(ESTATFECHA)=WYEAR.AND.MONTH(ESTATFECHA)<WMONTH))
      IF  ESTATUS = "CO"
         IF TIPOPE="DP".OR.TIPOPE="NC"
            STORE WSALDOINI + MONTO TO WSALDOINI
         ELSE
            IF TIPOPE="CH".OR.TIPOPE="ND"
               STORE WSALDOINI - MONTO TO WSALDOINI
            ENDIF
         ENDIF
      ENDIF
      SKIP
   ENDDO
   STORE WLINE+1 TO WLINE
   IF WLINE>WSALTO
      DO BC0316H
   ENDIF
   @ WLINE,00 SAY "Saldo inicial Edo. De Cta."
   @ WLINE,65 SAY WSALDOINI PICTURE "####,###,###.##"

   *** MOVIMIENTOS CONCILIADOS DEL MES
   STORE WSALDOINI TO WSALDOFIN
   *** TOMA SOLO REGISTROS CONCILIADOS DEL MES EN CONCILIACION
   SELECT 2
   STORE WCUENTA+STR(WYEAR)+STR(WMONTH) TO WCLAVE
   SEEK WCLAVE
   DO WHILE .NOT. EOF() .AND. CTA = WCUENTA ;
            .AND. (YEAR(ESTATFECHA)=WYEAR.AND.MONTH(ESTATFECHA)=WMONTH)
      IF ESTATUS <> "CO"
         SELECT 2
         SKIP
         LOOP
      ENDIF
      IF TIPOPE="DP".OR.TIPOPE="NC"
         STORE WSALDOFIN + MONTO TO WSALDOFIN
      ELSE
         IF TIPOPE="CH".OR.TIPOPE="ND"
            STORE WSALDOFIN - MONTO TO WSALDOFIN
         ENDIF
      ENDIF
      STORE WLINE + 1 TO WLINE
      IF WLINE>WSALTO
         DO BC0316H
      ENDIF
      @ WLINE,00 SAY ESTATFECHA
      @ WLINE,11 SAY TIPOPE
      @ WLINE,14 SAY NUMOPE
      IF TIPOPE="CH".OR.TIPOPE="ND"
         @ WLINE,27 SAY MONTO PICTURE "####,###,###.##"
      ELSE
         IF TIPOPE="DP".OR.TIPOPE="NC"
            @ WLINE,47 SAY MONTO PICTURE "####,###,###.##"
         ENDIF
      ENDIF
      @ WLINE,65 SAY WSALDOFIN PICTURE "####,###,###.##"
      SKIP
   ENDDO
   STORE WLINE + 1 TO WLINE
   IF WLINE>WSALTO
      DO BC0316H
   ENDIF
   @ WLINE,00 SAY "Saldo final Edo. De Cta."
   @ WLINE,65 SAY WSALDOFIN PICTURE "####,###,###.##"
   STORE WLINE + 1 TO WLINE
   @ WLINE,00 SAY REPLICATE("-",80)

   *** CALCULA SALDO DEL SISTEMA AL MES DE CONCILIACION
   STORE 0 TO WSALDOSYS
   SELECT 2
   SET ORDER TO 1
   SEEK WCUENTA
   DO WHILE .NOT. EOF() .AND. CTA = WCUENTA
      IF ESTATUS = "AN"
         SELECT 2
         SKIP
         LOOP
      ENDIF
      IF (YEAR(FECHA)<WYEAR) .OR. (YEAR(FECHA)=WYEAR.AND.MONTH(FECHA)<=WMONTH)
         IF TIPOPE = "DP" .OR. TIPOPE = "NC"
            STORE WSALDOSYS + MONTO TO WSALDOSYS
         ELSE
            IF TIPOPE = "CH" .OR. TIPOPE = "ND"
               STORE WSALDOSYS - MONTO TO WSALDOSYS
            ENDIF
         ENDIF
      ELSE
         EXIT
      ENDIF
      SELECT 2
      SKIP
   ENDDO

   *** MOVIMIENTOS NO CONCILIADOS HASTA EL MES
   STORE WLINE + 1 TO WLINE
   IF WLINE>WSALTO
      DO BC0316H
   ENDIF
   @ WLINE,00 SAY "CONCILIACION"
   STORE WLINE + 1 TO WLINE
   IF WLINE>WSALTO
      DO BC0316H
   ENDIF
   @ WLINE,00 SAY "Saldo final no conciliado"
   @ WLINE,65 SAY WSALDOSYS PICTURE "####,###,###.##"
   STORE WSALDOSYS TO WSALDOCON
   *** TOMA SOLO REGISTROS NO CONCILIADOS HASTA EL MES DE CONCILIACION
   *** O CONCILIADOS CON FECHA POSTERIOR A LA DEL PROCESO
   SELECT 2
   SET ORDER TO 1
   SEEK WCUENTA
   DO WHILE .NOT. EOF() .AND. CTA = WCUENTA ;
            .AND. ((YEAR(FECHA)<WYEAR) .OR. (YEAR(FECHA)=WYEAR.AND.MONTH(FECHA)<=WMONTH))
      IF ESTATUS = "CO"
         IF YEAR(ESTATFECHA)>WYEAR .OR. (YEAR(ESTATFECHA)=WYEAR.AND.MONTH(ESTATFECHA)>WMONTH)
            *** PASA BIEN
         ELSE
            SELECT 2
            SKIP
            LOOP
         ENDIF
      ELSE
         IF ESTATUS = "AN"
            SELECT 2
            SKIP
            LOOP
         ENDIF
      ENDIF
      IF TIPOPE="DP".OR.TIPOPE="NC"
         STORE WSALDOCON - MONTO TO WSALDOCON
      ELSE
         IF TIPOPE="CH".OR.TIPOPE="ND"
            STORE WSALDOCON + MONTO TO WSALDOCON
         ENDIF
      ENDIF
      STORE WLINE + 1 TO WLINE
      IF WLINE>WSALTO
         DO BC0316H
      ENDIF
      @ WLINE,00 SAY FECHA
      @ WLINE,11 SAY TIPOPE
      @ WLINE,14 SAY NUMOPE
      IF TIPOPE="CH".OR.TIPOPE="ND"
         @ WLINE,47 SAY MONTO PICTURE "####,###,###.##"
      ELSE
         IF TIPOPE="DP".OR.TIPOPE="NC"
            @ WLINE,27 SAY MONTO PICTURE "####,###,###.##"
         ENDIF
      ENDIF
      @ WLINE,65 SAY WSALDOCON PICTURE "####,###,###.##"
      SKIP
   ENDDO
   STORE WLINE + 1 TO WLINE
   IF WLINE>WSALTO
      DO BC0316H
   ENDIF
   @ WLINE,00 SAY "Saldo final conciliado"
   @ WLINE,65 SAY WSALDOCON PICTURE "####,###,###.##"
   STORE WLINE + 1 TO WLINE
   @ WLINE,00 SAY REPLICATE("-",80)

   STORE WSALDOCON - WSALDOFIN TO WDIFEREN
   STORE WLINE + 1 TO WLINE
   IF WLINE>WSALTO
      DO BC0316H
   ENDIF
   @ WLINE,00 SAY "DIFERENCIA CONCILIADO - EDO. DE CTA."
   @ WLINE,65 SAY WDIFEREN PICTURE "####,###,###.##"

   IF WSALIDA = "M"
      STORE "OPRIMA <ENTER> PARA CONTINUAR" TO MES
      DO AVISO WITH MES
   ELSE
      EJECT
      SET DEVI TO SCRE
   ENDIF
   EXIT
ENDDO
CLOSE DATA
CLOSE INDEX
RETURN

